plugins {
	// Fabric loom
	id "fabric-loom" version "1.1-SNAPSHOT" apply false
	// Forge Gradle
	id 'net.minecraftforge.gradle' version '5.1.+' apply false

	id 'com.matthewprenger.cursegradle' version '1.4.0' apply false
	id "com.modrinth.minotaur" version "2.+" apply false
	// Dokka docs
	id "org.jetbrains.kotlin.jvm" version "1.5.31"
	id("org.jetbrains.dokka") version "1.5.30"
}

repositories {
	mavenCentral()
	maven {
		url 'https://jitpack.io'
	}
	maven { url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven" }
}

allprojects {
	apply plugin: "java"
	apply plugin: "maven-publish"

	archivesBaseName = rootProject.archives_base_name + "-" + project.name + "-" + rootProject.minecraft_version
	version = rootProject.mod_version
	group = rootProject.maven_group

	repositories {
		mavenCentral()
		maven {
			url 'https://jitpack.io'
		}
		maven { url 'https://maven.nucleoid.xyz' }
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"

		def javaTarget = 17
		it.options.release = javaTarget

		sourceCompatibility = JavaVersion.toVersion(javaTarget)
		targetCompatibility = JavaVersion.toVersion(javaTarget)

		if (JavaVersion.current() < JavaVersion.toVersion(javaTarget)) {
			toolchain.languageVersion.set(JavaLanguageVersion.of(javaTarget))
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifactId = rootProject.archives_base_name + "-" + project.name
				from components.java
			}
		}

		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		repositories {
			// Add repositories to publish to here.
			// Notice: This block does NOT have the same function as the block in the top level.
			// The repositories here will be used for publishing your artifact, not for
			// retrieving dependencies.
			mavenLocal()
		}
	}
}

subprojects {
	// Dokka docs
	apply plugin: "org.jetbrains.kotlin.jvm"
	apply plugin: "org.jetbrains.dokka"

	// Publishing plugins
	if (it.name != "common") {
		apply plugin: "com.modrinth.minotaur"
		apply plugin: "com.matthewprenger.cursegradle"
	}

	if (it.name != "forge") {
		apply plugin: "fabric-loom"
		repositories {
			maven {
				url 'https://oss.sonatype.org/content/repositories/snapshots'
			}
			maven {
				url 'https://masa.dy.fi/maven'
			}
		}

		loom {
			accessWidenerPath = file("${project.rootDir}/common/src/main/resources/taterzens.accesswidener")
		}

		dependencies {
			// To change the versions see the gradle.properties file
			modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"
			modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
			minecraft "com.mojang:minecraft:${project.minecraft_version}"
			mappings loom.layered {
				//mappings("net.fabricmc:yarn:$project.yarn_mappings:v2")
				it.officialMojangMappings()
			}

			// C2B
			modImplementation("com.github.samolego.Config2Brigadier:config2brigadier-fabric:${rootProject.c2b_version}")


			// Disguiselib
			modImplementation("xyz.nucleoid:disguiselib-fabric:${project.disguiselib_version}")
		}
	}

	if (it.name != "common") {
		dependencies {
			compileOnly project(":common")
		}

		processResources {
			from project(":common").sourceSets.main.resources
		}

		tasks.withType(JavaCompile) {
			source(project(":common").sourceSets.main.allSource)
		}
	}

	tasks.withType(dokkaHtmlPartial.getClass()).configureEach {
		pluginsMapConfiguration.set(
				["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
		)
	}

	jar {
		from("LICENSE") {
			rename { "${it}_${project.archivesBaseName}" }
		}
		manifest {
			attributes([
					"Specification-Title"     : rootProject.archives_base_name,
					"Specification-Vendor"    : "",
					"Specification-Version"   : rootProject.mod_version,
					"Implementation-Title"    : project.name,
					"Implementation-Version"  : project.jar.archiveVersion,
					"Implementation-Vendor"   : "",
					"MixinConfigs"            : "${rootProject.archives_base_name}.mixins.json",
					"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
			])
		}
	}
}
