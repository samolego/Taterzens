def ENV = System.getenv()

dependencies {
    // Carpet
    modCompileOnly("com.github.gnembon:fabric-carpet:${project.carpet_core_version}")

    // Patbox's library for registry sync manipulation
    modImplementation include("eu.pb4:polymer-reg-sync-manipulator:${project.polymer_registry_sync_version}")

    // Permission API
    modImplementation(include('me.lucko:fabric-permissions-api:0.1-SNAPSHOT'))

    // Server translations
    modImplementation include("xyz.nucleoid:server-translations-api:${project.translations_api}")

    // SGUI
    modImplementation(include("eu.pb4:sgui:${project.sgui_version}"))

    // C2B
    include("com.github.samolego.Config2Brigadier:config2brigadier-fabric:${rootProject.c2b_version}")

    // Disguiselib
    modImplementation("xyz.nucleoid:disguiselib-fabric:${project.disguiselib_version}")
}

processResources {
    inputs.property "version", rootProject.version

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.version
    }
    filesMatching("taterzens.common.mixins.json") {
        expand "refmap": "taterzens-${project.name}-${rootProject.minecraft_version}-refmap.json"
    }
}

// from FAPI https://github.com/FabricMC/fabric/blob/1.16/build.gradle
curseforge {
    if (ENV.CURSEFORGE_API_KEY) {
        apiKey = ENV.CURSEFORGE_API_KEY
    }

    project {
        id = "446499"
        changelog = ENV.CHANGELOG ?: "A changelog can be found at https://github.com/samolego/Taterzens/releases/tag/${version}"
        releaseType = "release"
        addGameVersion "${project.minecraft_version}"
        addGameVersion "Fabric"
        addGameVersion "Quilt"

        mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}-fabric.jar")) {
            displayName = "[${project.minecraft_version}] Taterzens ${version}"
        }

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}

modrinth {
    token = ENV.MODRINTH_TOKEN
    projectId = "vE972Kux"
    versionNumber = project.minecraft_version + "+" + version + "-fabric"
    versionType = "release"
    changelog = ENV.CHANGELOG ?: "A changelog can be found at https://github.com/samolego/Taterzens/releases/tag/${version}"
    versionName = "[${project.minecraft_version}] Taterzens ${version} [Fabric]"

    uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}-fabric.jar")

    gameVersions = ["${project.minecraft_version}"]
    loaders = ['fabric', 'quilt']

    dependencies { // A special DSL for creating dependencies
        required.project "fabric-api" // Creates a new required dependency on Fabric API
    }
}
